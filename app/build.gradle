apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'com.betomorrow.appcenter'
apply plugin: 'androidx.navigation.safeargs.kotlin'

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "de.bornholdtlee.defaultprojectkotlin"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode System.getenv("CI_PIPELINE_ID") as Integer ?: 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding true
    }

    signingConfigs {
        release {
            storeFile file('keystore/ENTER_KEYSTORE_NAME_AND_DONT_FORGET_THE_VARIABLES_IN_GITLAB')
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            android.applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    outputFileName = "${variant.name}-${variant.versionName}-${versionCode}.apk"
                }
            }

            signingConfig signingConfigs.release
        }
    }


    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    flavorDimensions("app")
    productFlavors {
        dev {
            dimension = "app"
            applicationId "dev.de.bornholdtlee.defaultproject"
            buildConfigField 'String', 'BASE_URL', '"https://api.stackexchange.com"'
        }

        prod {
            dimension = "app"
            applicationId "de.bornholdtlee.defaultproject"
            buildConfigField 'String', 'BASE_URL', '"https://api.stackexchange.com"'
        }
    }
}

ktlint {
    version = "0.35.0"
    debug = true
    verbose = true
    android = false
    outputToConsole = true
    disabledRules = ["no-wildcard-imports", "import-ordering"]
    reporters {
        reporter "plain"
        reporter "checkstyle"
    }
    ignoreFailures = false
    kotlinScriptAdditionalPaths { include fileTree("scripts/") }
    filter {
        exclude("**/generated/**")
        include("**/kotlin/**")
    }
}

appcenter {
    ownerName = "OWNER_EINTRAGEN"
    distributionGroups = ["Collaborators"]
    notifyTesters = false
    apps {
        prodRelease {
            appName = "APP_NAME_EINTRAGEN"
            apiToken = "API_TOKEN_EINTRAGEN"
        }
        devRelease {
            appName = "APP_NAME_EINTRAGEN"
            apiToken = "API_TOKEN_EINTRAGEN"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.core:core-ktx:1.5.0-alpha05'

    ///// ANDROID /////
    implementation 'com.google.android.material:material:1.3.0-alpha03'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha06'
    implementation 'androidx.exifinterface:exifinterface:1.3.1'
    implementation 'androidx.fragment:fragment-ktx:1.2.5'
    implementation 'androidx.activity:activity-ktx:1.1.0'
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.2.0"
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.1"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.1"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
    implementation 'androidx.security:security-crypto:1.1.0-alpha02'

    /////DATABASE/////
    implementation "androidx.room:room-ktx:2.2.5"
    implementation "androidx.room:room-rxjava2:2.2.5"
    implementation "androidx.room:room-runtime:2.2.5"
    kapt "androidx.room:room-compiler:2.2.5"

    ////DEPENDENCY INJECTION////
    implementation 'org.koin:koin-android:2.1.6'

    /////API/////
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    /////OTHER/////
    implementation 'com.jakewharton.threetenabp:threetenabp:1.3.0'
    implementation "com.facebook.stetho:stetho:1.5.1"
    implementation "com.facebook.stetho:stetho-okhttp3:1.5.1"

    ///// TESTING /////
    testImplementation "org.robolectric:robolectric:4.4"
    testImplementation "org.robolectric:shadows-multidex:4.4"
    testImplementation "io.mockk:mockk:1.10.2"
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // https://github.com/JakeWharton/ThreeTenABP/issues/14
    testImplementation('org.threeten:threetenbp:1.5.0') {
        exclude group: 'com.jakewharton.threetenabp', module: 'threetenabp'
    }
}
